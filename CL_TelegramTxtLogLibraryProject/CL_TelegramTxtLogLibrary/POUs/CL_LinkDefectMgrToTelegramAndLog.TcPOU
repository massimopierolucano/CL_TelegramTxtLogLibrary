<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="CL_LinkDefectMgrToTelegramAndLog" Id="{bd3abd17-e119-4492-88fb-5fb9a74ccd54}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK CL_LinkDefectMgrToTelegramAndLog EXTENDS CL_MasterRootClass
VAR
  bInit                   : BOOL := FALSE;
	_DefectMngr             : REFERENCE TO CL_DefectManager;
  _TelegramBOT            : REFERENCE TO CL_TelegramBOT;
  _TxtLog                 : REFERENCE TO CL_TextLog;
  _ActTime                :	REFERENCE TO CL_ActTimeDate;
  
  indDfList					      : UINT := 1;

  _stConfigMsgBuild       : ST_ConfigMsgBuild;
  _stShow                 : ST_ShowMsgFields;
  
  _bShowRegisterMsg       : BOOL := FALSE;
  _bAllDefectsRegistered  : BOOL := FALSE;
	
	sTelegramMsg_P	        : POINTER TO T_StringBuffer;//used to generate very long Telegram messages limited to 3500 chars
	sTextOut_P              : POINTER TO T_StringBuffer;//used to generate short ADS console messages limited to 255 chars
  
	pDf			      		      : POINTER TO CL_Defect;
	iDf		        		      : ITF_Defect;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[//IF bInit and _bAllDefectsRegistered THEN
IF bInit THEN
  this^.Run(); 
END_IF]]></ST>
    </Implementation>
    <Method Name="AddDateTime" Id="{4095967f-1e6c-4d2d-9fdf-f031db0e2961}">
      <Declaration><![CDATA[METHOD PRIVATE AddDateTime
VAR_IN_OUT
	sMsgTimeCreation	: T_MaxString;
END_VAR
VAR
  sDatetime         : T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[(*
sBOTMsgTimeCreation^ := DT_TO_STRING(_objActTime_R.GetDateTime);
sBOTMsgTimeCreation^ := REPLACE(sBOTMsgTimeCreation^,' ',1,3);
sBOTMsgTimeCreation^ := CONCAT(' ',sBOTMsgTimeCreation^);
//sBOTMsgTimeCreation^ := CONCAT(sBOTMsgTimeCreation^,'%0A');
sBOTMsgTimeCreation^ := CONCAT(sBOTMsgTimeCreation^,'  ');
*)

(*
sMsgTimeCreation := concat(sMsgTimeCreation,_ActTime.GetStrDateTime());//2024-10-15-23:16:25
sMsgTimeCreation := REPLACE(sMsgTimeCreation,' ',1,11);
sMsgTimeCreation := CONCAT('Date: ', sMsgTimeCreation);
sMsgTimeCreation := CONCAT(sMsgTimeCreation,'  ');
*)

sMsgTimeCreation := CONCAT(sMsgTimeCreation,'  Date: ');
sDatetime := _ActTime.GetStrDateTime();//2024-10-15-23:16:25
sDatetime := REPLACE(sDatetime,' ',1,11);
sMsgTimeCreation := CONCAT(sMsgTimeCreation,sDatetime);]]></ST>
      </Implementation>
    </Method>
    <Property Name="AllDefectsRegistered" Id="{1c8b8e73-0d3a-484b-9edf-010e8c63c25b}">
      <Declaration><![CDATA[PROPERTY PUBLIC AllDefectsRegistered : BOOL]]></Declaration>
      <Get Name="Get" Id="{62fe80e3-e684-4641-a14c-58c7f66c300c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[AllDefectsRegistered := _bAllDefectsRegistered;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{79d2daf0-2965-41ad-bffe-6b518db19546}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_bAllDefectsRegistered := AllDefectsRegistered;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="BuildMsg" Id="{a5846a05-374c-4f5a-b86f-1042d24e98b9}">
      <Declaration><![CDATA[METHOD PRIVATE BuildMsg : BOOL
VAR_INPUT
  MsgType       : E_MsgType;
END_VAR
VAR
  sStartMsg     : T_MAXSTRING;
  
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//%0A telegram new line
CASE MsgType OF
  E_MsgType.Alarm:
    sStartMsg := _stConfigMsgBuild.AlarmMsg.sStartMsg;
    _stShow := _stConfigMsgBuild.AlarmMsg;
  E_MsgType.Info:
    sStartMsg := _stConfigMsgBuild.InfoMsg.sStartMsg;
    _stShow := _stConfigMsgBuild.InfoMsg;
  E_MsgType.Register:
    sStartMsg := _stConfigMsgBuild.RegisterMsg.sStartMsg;
    _stShow := _stConfigMsgBuild.RegisterMsg;
END_CASE   
sTelegramMsg_P^ := '';
sTextOut_P^ := '';

BuildMsg := pDf^.BuildStringMsg(sMsgOut_P := sTelegramMsg_P, sTextOut_P := sTextOut_P, stShow := _stShow);
                                      
IF _stShow.bDateTime THEN 
  AddDateTime(sStartMsg);
END_IF  

IF _stShow.bTelegram THEN
  CONCAT2(ADR(sStartMsg),sTelegramMsg_P,sTelegramMsg_P,SIZEOF(sTelegramMsg_P^));
END_IF

IF _stShow.bTxtLog THEN
  CONCAT2(ADR(sStartMsg),sTextOut_P,sTextOut_P,SIZEOF(sTextOut_P^));
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Init" Id="{e53ab38c-96f3-4595-9dcb-80606fd15102}">
      <Declaration><![CDATA[METHOD PUBLIC Init : BOOL
VAR_INPUT
  stIrrLine        : ST_LineReference;
  DefectMngr       : REFERENCE TO CL_DefectManager;
  TelegramBOT      : REFERENCE TO CL_TelegramBOT;
  TxtLog           : REFERENCE TO CL_TextLog;
  ActTime          : REFERENCE TO CL_ActTimeDate;
  stConfigMsg      : ST_ConfigMsgBuild;
END_VAR
VAR
  bufferSize       : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bInit := Init := FALSE;
IF __ISVALIDREF(DefectMngr) AND __ISVALIDREF(TelegramBOT) AND __ISVALIDREF(TxtLog) and __IsValidRef(ActTime) THEN 
	_DefectMngr REF= DefectMngr;
  _TelegramBOT REF= TelegramBOT;
  _TxtLog REF= TxtLog;
  _ActTime REF= ActTime;
  
  MRLineRef := stIrrLine;

  indDfList := 1;
	_stConfigMsgBuild := stConfigMsg;
  _bShowRegisterMsg := _stConfigMsgBuild.bShowRegisterMsg;
  
  bufferSize := SIZEOF(T_StringBuffer)+4;
  sTelegramMsg_P := __NEW(BYTE, bufferSize);
  sTextOut_P := __NEW(BYTE, bufferSize);

  bInit := Init := sTelegramMsg_P <> 0 AND sTextOut_P <> 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Property Name="RegisterAllMsgSent" Id="{8ce1c373-3a44-4ce3-99de-0f2c96643762}">
      <Declaration><![CDATA[PROPERTY PUBLIC RegisterAllMsgSent : BOOL]]></Declaration>
      <Get Name="Get" Id="{83921c70-fa44-461e-8e77-81aec684960c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[RegisterAllMsgSent := not _bShowRegisterMsg;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Release" Id="{f084cedf-9488-4ccc-9979-c6f3f39bdb57}">
      <Declaration><![CDATA[METHOD PUBLIC Release : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bInit := FALSE;
IF sTelegramMsg_P <> 0  THEN 
  __DELETE(sTelegramMsg_P);
END_IF

IF sTextOut_P <> 0  THEN 
  __DELETE(sTelegramMsg_P);
END_IF

Release := sTelegramMsg_P = 0 AND sTextOut_P = 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Run" Id="{d1739173-161d-4fc4-ba51-c3859ce315dc}">
      <Declaration><![CDATA[METHOD PRIVATE Run
VAR
  bDefectToSend	: BOOL;
END_VAR

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _TelegramBOT.IsBusy AND NOT _TxtLog.IsBusy THEN  
	IF _DefectMngr.List(indDfList,0) <> 0 THEN
		iDf := _DefectMngr.List(indDfList,0)^;
		IF __QUERYPOINTER(iDf, pDf) THEN // Ctrl if address has the good interface (ITF_Default)
      pDf := _DefectMngr.List(indDfList,0);  
      IF _bShowRegisterMsg then
        RunRegister();
        IF (indDfList > _DefectMngr.nMaxDefects) THEN //all Defect Register Messages sent
          _bShowRegisterMsg := false;//Register all defects done; the object can send starting from now Alarm and Info messages
				END_IF
			ELSE
        bDefectToSend := pDf^.State = E_DefectState.Active OR pDf^.State = E_DefectState.ActiveAndAck;
        IF bDefectToSend THEN 
          RunAlarm();        
          RunInfo();
        END_IF//pDf^.State = E_DefectState.Active OR pDf^.State = E_DefectState.ActiveAndAck;
      END_IF
    END_IF//__QUERYPOINTER
  END_IF//_DefectMngr.List(indDfList,0) <> 0 

  //Telegram and TextLog not Busy --> increment at each cycle the Defect index
  indDfList := indDfList + 1;
  IF (indDfList > _DefectMngr.nMaxDefects) THEN	// Constant not accessible defect manager, or yes. Actually yes, but access through GVL not suitable!	
    indDfList := 1;
    _bShowRegisterMsg := false;//after first pass all register messages are sent, do not send anymore
  END_IF 
END_IF//TelegramBOT and TextLog both are not busy]]></ST>
      </Implementation>
    </Method>
    <Method Name="RunAlarm" Id="{71210b3a-a1f9-46bc-8caf-21060bf37ca6}">
      <Declaration><![CDATA[METHOD PRIVATE RunAlarm
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//TcEventSeverity : (Verbose:=0, Info:=1, Warning:=2, Error:=3, Critical:=4);
IF pDf^.CanSendAlarmMsg AND (pDf^.Severity > TcEventSeverity.Info) THEN //Telegram msg not yet sent   
  //Critical, Error and Warning Defect are sent to Alarm group
  IF BuildMsg(E_MsgType.Alarm) THEN
    IF _stShow.bTxtLog THEN 
      _TxtLog.RequWriteMsg(sTextOut_P);
    END_IF
    IF _stShow.bTelegram THEN
      _TelegramBOT.RequSendMsgToAlarm(sTelegramMsg_P);
    END_IF
  END_IF					
END_IF//pDf^.Severity... and pDf^.CanSendTelegramAlarmMsg
]]></ST>
      </Implementation>
    </Method>
    <Method Name="RunInfo" Id="{a768a1da-2322-4a89-aa68-b19e75165cb7}">
      <Declaration><![CDATA[METHOD PRIVATE RunInfo
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//TcEventSeverity : (Verbose:=0, Info:=1, Warning:=2, Error:=3, Critical:=4);
//sendMsgToAlarm and sendMsgToInfo cannot be executed together; sendMsgToInfo can be requested only once AlarmMsgAlreadySent is true and TelegramBOT is again not busy
//IF (pDf^.CanSendInfoMsg AND (pDf^.Severity <= TcEventSeverity.Info)) OR (pDf^.CanSendInfoMsg AND (pDf^.Severity > TcEventSeverity.Info) AND pDf^.AlarmMsgAlreadySent) THEN
IF (pDf^.CanSendInfoMsg AND (pDf^.Severity <= TcEventSeverity.Info)) THEN
  //Verbose and Info Defect are sent only to InfoDebug group
  IF BuildMsg(E_MsgType.Info) THEN
    IF _stShow.bTelegram THEN
      _TelegramBOT.RequSendMsgToInfo(sTelegramMsg_P);
    END_IF
    IF _stShow.bTxtLog THEN 
      _TxtLog.RequWriteMsg(sTextOut_P);
    END_IF
  END_IF
END_IF//pDf^.Severity... and pDf^.CanSendTelegramAlarmMsg]]></ST>
      </Implementation>
    </Method>
    <Method Name="RunRegister" Id="{78317fd1-ee34-4bb9-978c-0c545c1f7e8b}">
      <Declaration><![CDATA[METHOD PRIVATE RunRegister

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pDf^.CanSendRegisterMsg AND _bShowRegisterMsg THEN //all Defect registered are sent
  IF BuildMsg(E_MsgType.Register) THEN
   IF _stShow.bTxtLog THEN 
    _TxtLog.RequWriteMsg(sTextOut_P);
   END_IF
   IF _stShow.bTelegram THEN
    _TelegramBOT.RequSendMsgToInfo(sTelegramMsg_P);
   END_IF
  END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="TelegramMsgFilter" Id="{35d74495-d9fb-4c25-bcc9-f60944056ab5}">
      <Declaration><![CDATA[METHOD PRIVATE TelegramMsgFilter : BOOL
VAR_INPUT
	sDatabaseMsg	:	T_MaxString;
END_VAR
VAR_OUTPUT
	sTelegramMsg	:	T_MaxString;
END_VAR
VAR
	sTelegramTmp	:	T_MaxString;
	i:	INT;
	iSafe:	INT;
	iLengthStr:	INT	:= 0;
	iNonAsciiPos:	INT;
	iNumAsciiChar:	UDINT;

END_VAR

VAR CONSTANT
		// Language tables - hardcoded here since part of the algorithm, and meaningless to keep them outside.
	sASCIIcompatCharFR_AR : ARRAY [1..2,1..28] OF STRING := ['à','â','é','è','ê','ë','î','ï','ô','ù','û','ü','ÿ','ç',
																															'À','Â','É','È','Ê','Ë','Î','Ï','Ô','Ù','Û','Ü','Ÿ','Ç',
																															2('a'),4('e'),2('i'),'o',3('u'),'y','c',
																															2('A'),4('E'),2('I'),'O',3('U'),'Y','C'];
	sASCIIcompatCharDE_AR :	ARRAY [1..2,1..7] OF STRING  := ['Ä','ä','Ö','ö','Ü','ü','ß',
																															'Ae','ae','Oe','oe','Ue','ue','ss'];
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
sTelegramTmp := sDatabaseMsg;
IF NOT F_StringIsASCII(pSTRING := ADR(sTelegramTmp), nLen => iNumAsciiChar) THEN
	iLengthStr := LEN(sTelegramTmp);
//	IF C.sLangErrors = 'fr' THEN
	IF  _DefectMngr.ErrorDB.sMsgLang = 'fr' THEN
			FOR i:=1 TO 28 BY 1 DO
				iSafe := 0;
				REPEAT
					iNonAsciiPos := FIND(sTelegramTmp,sASCIIcompatCharFR_AR[1,i]);	// Table to store inside the object, as never used elsewhere and fixed sized.
					IF iNonAsciiPos <> 0 THEN
						sTelegramTmp := REPLACE(sTelegramTmp,sASCIIcompatCharFR_AR[2,i],1,iNonAsciiPos);
					END_IF
					iSafe := iSafe + 1;
				UNTIL iNonAsciiPos = 0 OR iSafe > iLengthStr
				END_REPEAT;	
			END_FOR
//	ELSIF C.sLangErrors = 'de' THEN
	ELSIF  _DefectMngr.ErrorDB.sMsgLang = 'de' THEN
		FOR i:=1 TO 7 BY 1 DO
		iSafe := 0;
			REPEAT
				FIND(sTelegramTmp,sASCIIcompatCharDE_AR[1,i]);
				IF iNonAsciiPos <> 0 THEN
					REPLACE(sTelegramTmp,sASCIIcompatCharDE_AR[2,i],1,iNonAsciiPos); // Caution, the length of the string is increasing
				END_IF
			iSafe := iSafe + 1;
			UNTIL iNonAsciiPos = 0 OR iSafe > 2*iLengthStr
			END_REPEAT
		END_FOR
	END_IF
END_IF
sTelegramMsg := sTelegramTmp;

//only to reproduce the bug without filtering
//sTelegramMsg := sDatabaseMsg;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>